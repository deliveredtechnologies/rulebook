buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id "com.github.kt3k.coveralls" version "2.8.0"
}

def projectUrl = properties['project.url']
def projectYear = properties['project.inceptionYear']
def projectScmUrl = properties['project.scm.url']
def projectScmConnection = properties['project.scm.connection']
def projectLicenseName = properties['project.license.name']
def projectLicenseUrl = properties['project.license.url']
def projectLicenseDistro = properties['project.license.distribution']
def projectIssueMgmtSystem = properties['project.issuemgmt.system']
def projectIssueMgmtUrl = properties['project.issuemgmt.url']
def projectDistMgmtId = properties['project.distributionmgmt.site.id']
def projectDistMgmtUrl = properties['project.distributionmgmt.site.url']

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    repositories {
        jcenter()
        mavenCentral()
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

subprojects {

    apply plugin: 'idea'
    apply plugin: 'checkstyle'
    apply plugin: 'com.bmuschko.nexus'

    group = properties['project.group']
    version = properties['project.version']
    sourceCompatibility = properties['project.compatibility']
    targetCompatibility = properties['project.compatibility']

    dependencies {

        //logging
        compile("org.slf4j:slf4j-api:1.7.22")
        //reflection
        compile("org.reflections:reflections:0.9.10")
        //unit testing
        testCompile("junit:junit:4.12")
        testCompile("net.jodah:concurrentunit:0.4.2")
        //for mocking
        testCompile("org.mockito:mockito-all:1.10.19")
        testRuntime("cglib:cglib-nodep:3.1")
        testRuntime("org.objenesis:objenesis:2.1")
    }

    compileJava.doFirst {
        println "source compatibility  " + sourceCompatibility
        println "target compatibility  " + targetCompatibility
    }

    jacocoTestReport {
        additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
        sourceDirectories = files(sourceSets.main.allSource.srcDirs)
        classDirectories =  files(sourceSets.main.output)
        reports {
            xml.enabled true
            csv.enabled false
            html.destination file("${buildDir}/reports/jacoco/test/")
        }
    }

    nexus {
        sign = true
    }

    checkstyle {
        configFile = new File("${rootDir}/config/checkstyle/checkstyle.xml")
        toolVersion = '6.14'
        ignoreFailures = false
    }

    task checkstyleMainHtml {
        doLast {
            ant.xslt(in: checkstyleMain.reports.xml.destination,
                    style: file('../config/checkstyle/checkstyle.xsl'),
                    out: new File("${buildDir}/reports/checkstyle", 'main.html'))
        }
    }

    task checkstyleTestHtml {
        doLast {
            ant.xslt(in: checkstyleTest.reports.xml.destination,
                    style: file('../config/checkstyle/checkstyle.xsl'),
                    out: new File("${buildDir}/reports/checkstyle", 'test.html'))
        }
    }

    checkstyleMainHtml.dependsOn checkstyleMain
    checkstyleTestHtml.dependsOn checkstyleTest
    build.dependsOn jacocoTestReport, checkstyleMainHtml, checkstyleTestHtml
}

project("rulebook-core") {
    modifyPom {
        project {
            name 'RuleBook'
            description 'A simple and intuitive rules abstraction for Java'
            url "${projectUrl}"
            inceptionYear "${projectYear}"

            scm {
                url "${projectScmUrl}"
                connection "${projectScmConnection}"
                developerConnection "${projectScmConnection}"
            }

            distributionManagement {
                site {
                    id "${projectDistMgmtId}"
                    url "${projectDistMgmtUrl}"
                }
            }

            issueManagement {
                system "${projectIssueMgmtSystem}"
                url "${projectIssueMgmtUrl}"
            }

            licenses {
                license {
                    name "${projectLicenseName}"
                    url "${projectLicenseUrl}"
                    distribution "${projectLicenseDistro}"
                }
            }

            developers {
                developer {
                    id 'Clayton7510'
                    name 'Clayton Long'
                    url 'https://github.com/Clayton7510'
                    email 'Clayton7510@gmail.com'
                    roles {
                        role 'Author'
                    }
                }
            }

            contributors {
                contributor {
                    name 'Nicholas Lambert'
                    url 'https://github.com/NicolasLambert'
                    roles {
                        role 'code contributor for v0.2.1'
                        role 'raised dsl enhancement for v0.8'
                    }
                }
                contributor {
                    name 'wheezymustafa'
                    url 'https://github.com/wheezymustafa'
                    roles {
                        role 'raised bugs and enhancement suggestions for v0.3 and v0.3.1'
                    }
                }
                contributor {
                    name 'Alessandro Torrisi'
                    url 'https://github.com/typhoon1978'
                    roles {
                        role 'raised bug for v0.5.1'
                    }
                }
                contributor {
                    name 'Marc Seeger'
                    url 'https://github.com/Gersee'
                    roles {
                        role 'raised bug for v0.5.1'
                    }
                }
                contributor {
                    name 'abhineet27'
                    url 'https://github.com/abhineet27'
                    roles {
                        role 'raised bug for v0.6.1'
                        role 'raised bug for v0.6.2'
                    }
                }
                contributor {
                    name 'Darrell Merryweather'
                    url 'https://github.com/darrellme'
                    roles {
                        role 'raised rule chain enhancement for v0.8'
                    }
                }
                contributor {
                    name 'Kent Jones'
                    url 'https://github.com/kentjones'
                    roles {
                        role 'raised bug for v0.8.1'
                    }
                }
                contributor {
                    name 'Ivan Portugal'
                    url 'https://github.com/ivanbportugal'
                    roles {
                        role 'raised bug for v0.8.1'
                    }
                }
                contributor {
                    name 'Brad Egler'
                    url 'https://github.com/bradegler'
                    roles {
                        role 'upgraded gradle build for v0.9.1'
                    }
                }
                contributor {
                    name 'pjotre86'
                    url 'https://github.com/pjotre86'
                    roles {
                        role 'raised bug for v0.9.1'
                    }
                }
                contributor {
                    name 'Tomek Bielaszewski'
                    url 'https://github.com/tomekbielaszewski'
                    roles {
                        role 'corrected some errata in the README'
                    }
                }
                contributor {
                    name 'Abhijeet Joshi'
                    url 'https://github.com/jabhijeet'
                    roles {
                        role 'code contributor for v0.10'
                    }
                }
                contributor {
                    name 'Riz Joj'
                    url 'https://github.com/rizjoj'
                    roles {
                        role 'code contributor for v0.10'
                    }
                }
            }
        }
    }
}

project("rulebook-spring") {
    modifyPom {
        project {
            name 'RuleBook Spring'
            description 'Spring support for RuleBook'
            url "${projectUrl}"
            inceptionYear "${projectYear}"

            scm {
                url "${projectScmUrl}"
                connection "${projectScmConnection}"
                developerConnection "${projectScmConnection}"
            }

            distributionManagement {
                site {
                    id "${projectDistMgmtId}"
                    url "${projectDistMgmtUrl}"
                }
            }

            issueManagement {
                system "${projectIssueMgmtSystem}"
                url "${projectIssueMgmtUrl}"
            }

            licenses {
                license {
                    name "${projectLicenseName}"
                    url "${projectLicenseUrl}"
                    distribution "${projectLicenseDistro}"
                }
            }

            developers {
                developer {
                    id 'Clayton7510'
                    name 'Clayton Long'
                    email 'Clayton7510@gmail.com'
                }
            }
        }
    }
}

task jacocoRootReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    dependsOn subprojects.test
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories =  files(subprojects.sourceSets.main.output)
    executionData = files(subprojects.jacocoTestReport.executionData)
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
        xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
        html.destination file("${buildDir}/reports/jacoco/test/")
    }
}

task alljavadoc(type: Javadoc) {
    source subprojects.sourceSets.main.allSource
    classpath = files(subprojects.sourceSets.main.compileClasspath)
    destinationDir = file("${projectDir}/docs/javadocs")
}

task copySource(type: Copy) {
    from subprojects.sourceSets.main.allSource
    into "${rootDir}/src/main/java"
}
